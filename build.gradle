plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.11.0'
    id 'java'
}

defaultTasks 'clean','build'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
sourceCompatibility = 1.8
ext.rundeckPluginVersion= '1.2'
ext.rundeckVersion= '3.3.x'
ext.pluginClassNames='com.plugin.sshjplugin.SSHJNodeExecutorPlugin,com.plugin.sshjplugin.SSHJFileCopierPlugin'
ext.pluginName = 'SSHJ Plugin'
ext.pluginDescription ='SSH Node Executor and File Copier plugin based on SSHJ library.'


scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}

project.version = scmVersion.version

repositories {
    mavenLocal()
    mavenCentral()
}

configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    implementation{
        extendsFrom pluginLibs
    }
}

dependencies {

    pluginLibs group: 'com.hierynomus', name: 'sshj', version: '0.31.0', ext: 'jar'
    pluginLibs group: 'net.i2p.crypto', name: 'eddsa', version: '0.3.0', ext: 'jar'
    pluginLibs group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.61', ext: 'jar'
    pluginLibs group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.61', ext: 'jar'
    pluginLibs group: 'commons-io', name: 'commons-io', version: '2.6', ext: 'jar'
    pluginLibs group: 'net.sf.expectit', name: 'expectit-core', version: '0.9.0'

    implementation 'org.rundeck:rundeck-core:4.6.1-20220914'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'

    testImplementation 'junit:junit:4.12'
    testImplementation "org.codehaus.groovy:groovy-all:2.4.15"
    testImplementation "org.spockframework:spock-core:1.0-groovy-2.4"
    testImplementation "cglib:cglib-nodep:2.2.2"
    testImplementation group: 'org.objenesis', name: 'objenesis', version: '1.2'
}


// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}


jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
        attributes 'Rundeck-Plugin-Name' : pluginName
        attributes 'Rundeck-Plugin-Description' : pluginDescription
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '2.x+'
        attributes 'Rundeck-Plugin-Tags': 'java,executor'
        attributes 'Rundeck-Plugin-License': 'MIT'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion, 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"
        attributes 'Class-Path': "${libList} lib/rundeck-core-${rundeckVersion}.jar"
    }
}
//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

